start: contract_clause+

contract_clause: node_clause | type_clause

type_clause : "types" "{" type_declaration+ "}"

type_declaration : TYPE_NAME ":" type_declaration_part ";"

TYPE_NAME :  UPPER_NAME
type_declaration_part : function_declaration
                      | set
                      | sequence

node_clause: "node" NODE_NAME "{" inputs outputs topic_list (assume)* (guarantee)+   "}"

NODE_NAME: LETTER+

io_var : VAR_NAME ":" TYPE_NAME

VAR_NAME : LOWER_NAME

inputs : "inputs" "(" (input ("," input)*)?  ")" | ("input" "(" input ")")*

input : io_var

outputs :  "outputs" "(" (output ("," output)*)?  ")" | ("output" "(" output ")")*

output : io_var

topic_list : "topics" "(" (topic ("," topic)*)? ")"

topic: TYPE FOL_NAME ("matches" "(" FOL_NAME ")" )?


//ros types
TYPE: "bool"
    | "int8"
    | "uint8"
    | "int16"
    | "uint16"
    | "int32"
    | "uint32"
    | "int64"
    | "uint64"
    | "float32"
    | "float64"
    | "string"
    | "time"
    | "duration"
    | FOL_NAME "/" FOL_NAME

assume : ("assume" | "A") "(" formula ")"

guarantee : ("guarantee"| "G") "(" formula ")"

formula: atomic_formula -> atom
        | "not" formula -> negation
        | formula "and" formula -> and_form
        | formula "or" formula -> or_form
        | formula "->" formula -> implies
        | formula "<=>" formula -> iff
        | "(" formula ")" -> sub_formula
        | "forall" "(" formula_variables (";"|"|") formula ")" -> forall
        | "exists" "(" formula_variables (";"|"|") formula ")" -> exists
        | "exists_unique" "(" formula_variables (";"|"|") formula ")" -> exists_unique

atomic_formula  : BOOLEAN
                | term "==" term -> equals
                | term "!=" term -> not_equals
                | term "in" term -> in_form
                | term "!in" term -> not_in
                | term "<=" term -> leq
                | term ">=" term -> geq
                | term "<" term -> lt
                | term ">" term -> gt
                | term COMPARE_OP term COMPARE_OP term -> var_range
                | FOL_NAME "(" terms ")" -> function_application



function_declaration: function_input "-->" function_output
function_input : BUILTIN ("x" BUILTIN)*
function_output: BUILTIN ("x" BUILTIN)*

COMPARE_OP : "<=" | ">=" | "<" | ">"

arith : (VARIABLE|NUMBER) ARITH_OP (VARIABLE|NUMBER)

ARITH_OP: "+" | "-" | "*" | "/"

BOOLEAN : "TRUE" | "FALSE"

BUILTIN : "REAL" | "INTEGER" | "NATURAL" | "BOOL"

terms : term ("," term)*

term : BOOLEAN
      | BUILTIN ("." BUILTIN)* -> term_builtins
      | VARIABLE
      | arith -> arithmetic
      | tuple
      | set
      | sequence
      | CONSTANT
      | FOL_NAME "(" terms ")" -> function_application
      | "\""  FOL_NAME "\""  -> string_literal


set : "{" (VARIABLE|set) ("," (VARIABLE|set))* "}"
    | "{}" -> empty_set

sequence : "<" VARIABLE ("," VARIABLE)* ">"
        | "<>" -> empty_sequence

tuple : "(" VARIABLE ("," VARIABLE)* ")"


variables : "(" (VARIABLE| BUILTIN) ("," (VARIABLE| BUILTIN))*  ")" -> tuple
          | (VARIABLE| BUILTIN) ("," (VARIABLE| BUILTIN))*

formula_variables : formula_variables_part (", " formula_variables_part)*

formula_variables_part : variables "in" (set|BUILTIN|VARIABLE) ("," (set|BUILTIN|VARIABLE))*


VARIABLE : FOL_NAME
CONSTANT: FOL_NAME

FOL_NAME: ("'"|"_"|LETTER|DIGIT)("'"|"_"|LETTER|DIGIT)*
LOWER_NAME: (LCASE_LETTER|DIGIT) ("'"|"_"|LETTER|DIGIT)*
UPPER_NAME: (UCASE_LETTER|DIGIT) ("'"|"_"|LETTER|DIGIT)*

%import common.DIGIT
%import common.LETTER
%import common.UCASE_LETTER
%import common.LCASE_LETTER


%import common.WORD
%import common.CPP_COMMENT
%import common.C_COMMENT
%import common.LETTER
%import common.INT -> NUMBER
%import common.WS

%ignore WS
%ignore CPP_COMMENT
%ignore C_COMMENT

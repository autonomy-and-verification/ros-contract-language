start: node_clause+

node_clause: "node" NODE_NAME "{" topic_list (assume)* (guarantee)+   "}"



NODE_NAME: LETTER+

topic_list : "topics" "(" (topic ("," topic)*)? ")"

topic: TYPE FOL_NAME


//ros types
TYPE: "bool"
    | "int8"
    | "uint8"
    | "int16"
    | "uint16"
    | "int32"
    | "uint32"
    | "int64"
    | "uint64"
    | "float32"
    | "float64"
    | "string"
    | "time"
    | "duration"
    | FOL_NAME "/" FOL_NAME

assume : ("assume" | "A") "(" formula ")"

guarantee : ("guarantee"| "G") "(" formula ")"

formula: atomic_formula -> atom
        | "not" formula -> negation
        | formula "and" formula -> and_form
        | formula "or" formula -> or_form
        | formula "->" formula -> implies
        | formula "<=>" formula -> iff
        | "(" formula ")" -> sub_formula
        | "forall" "(" formula_variables (";"|"|") formula ")" -> forall
        | "exists" "(" formula_variables (";"|"|") formula ")" -> exists
        | "exists_unique" "(" formula_variables (";"|"|") formula ")" -> exists_unique

atomic_formula  : BOOLEAN
                | term "==" term -> equals
                | term "!=" term -> not_equals
                | term "in" term -> in_form
                | term "!in" term -> not_in
                | term "<=" term -> leq
                | term ">=" term -> geq
                | term "<" term -> lt
                | term ">" term -> gt
                | term COMPARE_OP term COMPARE_OP term -> var_range
                | PRED_NAME "(" terms ")" -> predicate

COMPARE_OP : "<=" | ">=" | "<" | ">"

arith : (VARIABLE|NUMBER) ARITH_OP (VARIABLE|NUMBER)

ARITH_OP: "+" | "-" | "*" | "/"

BOOLEAN : "TRUE" | "FALSE"

BUILTIN : "REAL" | "INTEGER" | "NATURAL"

terms : term ("," term)*

term : BOOLEAN
      | BUILTIN ("." BUILTIN)* -> term_builtins
      | VARIABLE
      | arith -> arithmetic
      | tuple
      | set
      | sequence
      | CONSTANT
      | FUNC_NAME "(" terms ")" -> function
      | "\""  FOL_NAME "\""  -> string_literal


set : "{" (VARIABLE|set) ("," (VARIABLE|set))* "}"
    | "{}" -> empty_set

sequence : "<" VARIABLE ("," VARIABLE)* ">"
        | "<>" -> empty_sequence

tuple : "(" VARIABLE ("," VARIABLE)* ")"


variables : "(" (VARIABLE| BUILTIN) ("," (VARIABLE| BUILTIN))*  ")" -> tuple
          | (VARIABLE| BUILTIN) ("," (VARIABLE| BUILTIN))*

formula_variables : formula_variables_part (", " formula_variables_part)*

formula_variables_part : variables "in" (set|BUILTIN|VARIABLE) ("," (set|BUILTIN|VARIABLE))*


VARIABLE : FOL_NAME
CONSTANT: FOL_NAME


FUNC_NAME: (LCASE_LETTER|DIGIT) ("'"|"_"|LETTER|DIGIT)*
PRED_NAME: (UCASE_LETTER|DIGIT) ("'"|"_"|LETTER|DIGIT)*


FOL_NAME: ("'"|"_"|LETTER|DIGIT)("'"|"_"|LETTER|DIGIT)*

%import common.DIGIT
%import common.LETTER
%import common.UCASE_LETTER
%import common.LCASE_LETTER


%import common.WORD
%import common.CPP_COMMENT
%import common.C_COMMENT
%import common.LETTER
%import common.INT -> NUMBER
%import common.WS

%ignore WS
%ignore CPP_COMMENT
%ignore C_COMMENT

start: contract_clause+

contract_clause: node_clause | context_clause

context_clause : "context" "{" (type_declaration|constant_declaration)+ "}"

type_declaration : TYPE_NAME ":" type_declaration_part ";"

constant_declaration : TYPE_NAME "=" type_declaration_part ";"

TYPE_NAME :  UPPER_NAME
type_declaration_part : function_declaration
                      | set
                      | sequence
                      | tuple

node_clause: "node" NODE_NAME "{" inputs outputs topic_list (assume)* (guarantee)+   "}"

NODE_NAME: LETTER+

io_var : VAR_NAME ":" TYPE_NAME

VAR_NAME : LOWER_NAME

inputs : "inputs" "(" (input ("," input)*)?  ")"

input : io_var

outputs :  "outputs" "(" (output ("," output)*)?  ")"

output : io_var

topic_list : "topics" "(" (topic ("," topic)*)? ")"

topic: TYPE FOL_NAME ("matches" "(" topic_match_name ")" )?

topic_match_name : io_pointer? FOL_NAME
!io_pointer : "in." | "out."

//ros types
TYPE: "bool"
    | "int8"
    | "uint8"
    | "int16"
    | "uint16"
    | "int32"
    | "uint32"
    | "int64"
    | "uint64"
    | "float32"
    | "float64"
    | "string"
    | "time"
    | "duration"
    | FOL_NAME "/" FOL_NAME

assume : ("assume" | "A") "(" formula ")"

guarantee : ("guarantee"| "G") "(" formula ")"

formula: atomic_formula -> atom
        | "not" formula -> negation
        | formula "and" formula -> and_form
        | formula "or" formula -> or_form
        | formula "->" formula -> implies
        | formula "<=>" formula -> iff
        | "(" formula ")" -> sub_formula
        | "forall" "(" formula_variables (";"|"|") formula ")" -> forall
        | "exists" "(" formula_variables (";"|"|") formula ")" -> exists
        | "exists_unique" "(" formula_variables (";"|"|") formula ")" -> exists_unique

atomic_formula  : BOOLEAN
                | term "==" term -> equals
                | term "!=" term -> not_equals
                | term "in" term -> in_form
                | term "!in" term -> not_in
                | term "<=" term -> leq
                | term ">=" term -> geq
                | term "<" term -> lt
                | term ">" term -> gt
                | term COMPARE_OP term COMPARE_OP term -> var_range
                | variable_reference "(" terms ")" -> function_application



function_declaration: function_input "-->" function_output
function_input : (variable_reference| BUILTIN) ("x" (variable_reference| BUILTIN))*
function_output: (variable_reference| BUILTIN) ("x" (variable_reference| BUILTIN))*

COMPARE_OP : "<=" | ">=" | "<" | ">"

arith : (variable_reference|NUMBER) ARITH_OP (variable_reference|NUMBER)

ARITH_OP: "+" | "-" | "*" | "/"

BOOLEAN : "TRUE" | "FALSE"

BUILTIN : "REAL" | "INTEGER" | "NATURAL" | "BOOL"

terms : term ("," term)*

term : BOOLEAN
      | BUILTIN ("." BUILTIN)* -> term_builtins
      | variable_reference
      | topic_match_name
      | arith -> arithmetic
      | tuple
      | set
      | sequence
      | CONSTANT
      | function_application
      | "\""  FOL_NAME "\""  -> string_literal

function_application : variable_reference "(" terms ")"

set : "{" (variable_reference|set|function_application) ("," (variable_reference|set|function_application))* "}"
    | "{}" -> empty_set

UPTO : "upto"

sequence : "<" variable_reference ((UPTO variable_reference)? |("," variable_reference)*) ">"
        | "<>" -> empty_sequence


tuple : "(" variable_reference ("," variable_reference)* ")"



variables : "(" (variable_reference| BUILTIN) ("," (variable_reference| BUILTIN))*  ")" -> tuple
          | (variable_reference| BUILTIN) ("," (variable_reference| BUILTIN))*

formula_variables : formula_variables_part (", " formula_variables_part)*

formula_variables_part : variables "in" (set|BUILTIN|variable_reference) ("," (set|BUILTIN|variable_reference))*

variable_reference : io_pointer? VARIABLE
VARIABLE : FOL_NAME
CONSTANT: FOL_NAME

FOL_NAME: ("'"|"_"|LETTER|DIGIT)("'"|"_"|LETTER|DIGIT)*
LOWER_NAME: (LCASE_LETTER|DIGIT) ("'"|"_"|LETTER|DIGIT)*
UPPER_NAME: (UCASE_LETTER|DIGIT) ("'"|"_"|LETTER|DIGIT)*

%import common.DIGIT
%import common.LETTER
%import common.UCASE_LETTER
%import common.LCASE_LETTER


%import common.WORD
%import common.CPP_COMMENT
%import common.C_COMMENT
%import common.LETTER
%import common.INT -> NUMBER
%import common.WS

%ignore WS
%ignore CPP_COMMENT
%ignore C_COMMENT
